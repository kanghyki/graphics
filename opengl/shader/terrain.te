#version 430 core
layout (quads, fractional_odd_spacing, cw) in;
in vec2 TextureCoord[];
out float height;
out vec4 normal;
out vec4 position;

uniform sampler2D height_map;
uniform int height_map_width;
uniform int height_map_height;

uniform float height_scale;
uniform mat4 model;

#include "include/default.incl"
#define view t_view
#define proj t_proj

void main()
{
    float u = gl_TessCoord.x;
    float v = gl_TessCoord.y;

    vec2 t00 = TextureCoord[0];
    vec2 t01 = TextureCoord[1];
    vec2 t10 = TextureCoord[2];
    vec2 t11 = TextureCoord[3];

    vec2 t0 = (t01 - t00) * u + t00;
    vec2 t1 = (t11 - t10) * u + t10;
    vec2 texCoord = (t1 - t0) * v + t0;

    height = texture(height_map, texCoord).y;

    vec4 p00 = gl_in[0].gl_Position;
    vec4 p01 = gl_in[1].gl_Position;
    vec4 p10 = gl_in[2].gl_Position;
    vec4 p11 = gl_in[3].gl_Position;
    vec4 p0 = (p01 - p00) * u + p00;
    vec4 p1 = (p11 - p10) * u + p10;
    vec4 p = (p1 - p0) * v + p0;
    p += vec4(0.0, 1.0, 0.0, 0.0) * height * height_scale;
    position = model * p;
    gl_Position = proj * view * position;

    float tsx   = 1.0 / height_map_width;
    float tsy   = 1.0 / height_map_height;
    float left  = texture(height_map, texCoord + vec2(-tsx, 0.0)).y * height_scale * 2.0 - 1.0;
    float right = texture(height_map, texCoord + vec2(tsx, 0.0)).y * height_scale * 2.0 - 1.0;
    float up    = texture(height_map, texCoord + vec2(0.0, tsy)).y * height_scale * 2.0 - 1.0;
    float down  = texture(height_map, texCoord + vec2(0.0, -tsy)).y * height_scale * 2.0 - 1.0;
    normal = vec4(normalize(vec3(down - up, 2.0, left - right)), 1.0);
}