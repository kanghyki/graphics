struct MaterialForShading {
    vec3 albedo;
    float specular_alpha;
    float shininess;
};

vec3 BlinnPhong(vec3 light_strength, vec3 light_vec, vec3 normal, vec3 to_eye, MaterialForShading mat)
{
    vec3 halfway = normalize(to_eye + light_vec);
    float hdotn = dot(halfway, normal);
    vec3 specular = mat.albedo * pow(max(hdotn, 0.0f), mat.shininess);

    return (mat.albedo + (specular * mat.specular_alpha)) * light_strength;
}

vec3 ComputeDirectionalLight(Light L, MaterialForShading mat, vec3 normal, vec3 to_eye)
{
    vec3 light_vec = -L.direction;

    float ndotl = max(dot(light_vec, normal), 0.0f);
    vec3 lightStrength = L.strength * ndotl;

    return BlinnPhong(lightStrength, light_vec, normal, to_eye, mat);
}

float CalcAttenuation(float d, float falloff_start, float falloff_end)
{
    return clamp((falloff_end - d) / (falloff_end - falloff_start), 0.0, 1.0);
}

vec3 ComputePointLight(Light L, MaterialForShading mat, vec3 pos, vec3 normal, vec3 to_eye)
{
    vec3 light_vec = L.position - pos;
    float d = length(light_vec);
    if (d > L.falloff_end)
    {
        return vec3(0.0, 0.0, 0.0);
    }
    else
    {
        light_vec /= d;

        float ndotl = max(dot(light_vec, normal), 0.0f);
        vec3 lightStrength = L.strength * ndotl;

        float att = CalcAttenuation(d, L.falloff_start, L.falloff_end);
        lightStrength *= att;

        return BlinnPhong(lightStrength, light_vec, normal, to_eye, mat);
    }
}

vec3 ComputeSpotLight(Light L, MaterialForShading mat, vec3 pos, vec3 normal, vec3 to_eye)
{
    vec3 light_vec = L.position - pos;
    float d = length(light_vec);
    if (d > L.falloff_end)
    {
        return vec3(0.0f, 0.0f, 0.0f);
    }
    else
    {
        light_vec /= d;

        float ndotl = max(dot(light_vec, normal), 0.0f);
        vec3 light_strength = L.strength * ndotl;

        float att = CalcAttenuation(d, L.falloff_start, L.falloff_end);
        light_strength *= att;

        float spot_factor = pow(max(-dot(light_vec, L.direction), 0.0f), L.spot_power);
        light_strength *= spot_factor;

        return BlinnPhong(light_strength, light_vec, normal, to_eye, mat);
    }
}